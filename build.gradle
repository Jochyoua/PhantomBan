plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'io.github.jochyoua'
version = '1.0.3'

repositories {
    mavenCentral()
    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        name = "jeff-media"
        url = "https://repo.jeff-media.com/public/"
    }
}

dependencies {
    compileOnly "org.spigotmc:spigot-api:1.13-R0.1-SNAPSHOT"
    implementation group: 'net.jodah', name: 'expiringmap', version: '0.5.11'
    implementation "com.jeff_media:SpigotUpdateChecker:3.0.4"
}

def targetJavaVersion = 8
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

shadowJar {
    archiveClassifier.set('')
    dependencies {
        include(dependency('net.jodah:expiringmap'))
        include(dependency("com.jeff_media:"))
    }
    relocate('net.jodah.expiringmap', 'io.github.jochyoua.phantomban.shaded.expiringmap')
    relocate("com.jeff_media", "io.github.jochyoua.phantomban.shaded.jeff_media")
}


build.dependsOn shadowJar
